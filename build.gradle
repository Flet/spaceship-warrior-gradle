buildscript {

    ext.gdxVersion = "1.0-SNAPSHOT"
    ext.artemisVersion = "0.5.1-SNAPSHOT"

    repositories {
        maven {
            url 'https://github.com/steffenschaefer/gwt-gradle-plugin/raw/maven-repo/'
        }
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    }

    dependencies {
        classpath 'de.richsource.gradle.plugins:gwt-gradle-plugin:0.3'
        classpath 'com.android.tools.build:gradle:0.7.3+'
        classpath 'com.github.jtakakura:gradle-robovm-plugin:0.0.4'
        classpath "com.badlogicgames.gdx:gdx-tools:$gdxVersion"
        classpath "net.onedaybeard.artemis:artemis-odb:$artemisVersion"
        classpath "net.onedaybeard.artemis:artemis-odb-gwt:$artemisVersion"
        classpath "net.onedaybeard.artemis:artemis-odb-gwt:$artemisVersion:sources"
    }
}

allprojects {
    apply plugin: "eclipse"
    apply plugin: "idea"
    
    version = "1.0"
    ext.appName = "swg"

    // Uncomment line below with proper parameters to pass to TexturePacker
    // and TexturePacker will be invoked automagically whenever the target
    // "texurePacker" is invoked, e.g. "./gradlew texturePacker desktop:run"

    // ext.texturePacker = ['images', 'android/assets/data', 'gamegdx']
    
    repositories {
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        mavenLocal();
    }
}

project(":core") {
    apply plugin: "java"
    
    dependencies {
        compile "com.badlogicgames.gdx:gdx:$gdxVersion"
        compile "net.onedaybeard.artemis:artemis-odb:$artemisVersion"
    }
}

project(":desktop") {
    apply plugin: "java"
    apply plugin: "application"
    
    dependencies {
        compile project(":core")
        compile "com.badlogicgames.gdx:gdx-backend-lwjgl:$gdxVersion"
        compile "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
    }
}

project(":android") {
    apply plugin: "android"

    configurations { natives }

    dependencies {
        compile project(":core")
        compile "com.badlogicgames.gdx:gdx-backend-android:$gdxVersion"        
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-armeabi"
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-armeabi-v7a"
    }
}

project(":gwt") {
    apply plugin: "gwt"
    apply plugin: "war"
    webAppDirName = 'webapp'

    dependencies {
        compile project(":core")
        compile "com.badlogicgames.gdx:gdx-backend-gwt:$gdxVersion"
        compile "com.badlogicgames.gdx:gdx:$gdxVersion:sources"
        compile "com.badlogicgames.gdx:gdx-backend-gwt:$gdxVersion:sources"
        compile "net.onedaybeard.artemis:artemis-odb-gwt:$artemisVersion"
        compile "net.onedaybeard.artemis:artemis-odb-gwt:$artemisVersion:sources"
        compile "net.onedaybeard.artemis:artemis-odb:$artemisVersion"
        compile "net.onedaybeard.artemis:artemis-odb:$artemisVersion:sources"
    }

    gwt {
        gwtVersion='2.6.0' // Should match the gwt version used for building the gwt backend
        maxHeapSize="1G" // Default 256m is not enough for gwt compiler. GWT is HUNGRY
        minHeapSize="1G"

        src = files(file("src/")) // Needs to be in front of "modules" below.
        modules 'net.brokenspork.GdxDefinition'

        compiler {
            strict = true;
            enableClosureCompiler = true;
            disableClassMetadata = true;
            disableCastChecking = true;
        }
    }
}

project(":ios") {
    apply plugin: 'java'
    apply plugin: 'robovm'

    ext {
        mainClassName = "net.brokenspork.IOSLauncher"
        roboVMVersion = "0.0.9"
    }

    task wrapper(type: Wrapper) {
        gradleVersion = '1.9'
    }

    configurations { natives }

    dependencies {
        compile "org.robovm:robovm-rt:${roboVMVersion}"
        compile "org.robovm:robovm-cocoatouch:${roboVMVersion}"
        compile project(":core")
        compile "com.badlogicgames.gdx:gdx-backend-robovm:$gdxVersion"
        compile "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-ios"
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-ios"
    }
}


tasks.eclipse.doLast {
	delete ".project"
}
